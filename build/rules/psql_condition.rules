#vim: set syntax=rules:

init_code: {}

terminals:
  string {'\''.|'|*.'\''}
  integer {('-'+e).('0'+(<19>.d*))}
  float {('-'+e).('0'+(<19>.d*)).(('.'.d*)+e).(([eE].([+-]+e).d.d*)+e)}

  id {('_'+l).('_'+l+d)*}

  equal {"="+"=="}
  exclamation_equal {"!="+"<>"}
  rs_br {'>'}
  ls_br {'<'}
  rs_br_equal {">="}
  ls_br_equal {"<="}
  contains {"has"+"contains"+"like"+"HAS"+"CONTAINS"+"LIKE"}

  and {"&&"+"and"+"AND"}
  or {"||"+"or"+"OR"}
  not {"!"+"not"+"NOT"}

  lr_br {'('}
  rr_br {')'}

  _SKIP_ {w.w*}
  _SKIP__ {"--".!'\n'*.'\n'}
  _END_ {[\0;]}

nonterminals:
  <start>

  <B>
  <A>

  <cond>
  <const>
  <id>

rules:
  <start> -> <B> _END_ ->> {}

  $ -- exp operators --
  <B> -> <B> and <A> ->> {print("logical and")}
  <B> -> <B> or <A> ->> {print("logical or")}
  <B> -> <A> ->> {}

  <A> -> not <A> ->> {print("logical not")}
  <A> -> <cond> ->> {}

  <cond> -> <id> equal <const> ->> {print("operator =")}
  <cond> -> <id> exclamation_equal <const> ->> {print("operator !=")}
  <cond> -> <id> rs_br <const> ->> {print("operator >")}
  <cond> -> <id> ls_br <const> ->> {print("operator <")}
  <cond> -> <id> rs_br_equal <const> ->> {print("operator >=")}
  <cond> -> <id> ls_br_equal <const> ->> {print("operator <=")}
  <cond> -> <id> contains <const> ->> {print("operator contains")}

  <cond> -> lr_br <B> rr_br ->> {}

  <const> -> string ->> {print("string: "..rule_body(0))}
  <const> -> integer ->> {print("integer: "..rule_body(0))}
  <const> -> float ->> {print("float: "..rule_body(0))}

  <id> -> id ->> {print("id: "..rule_body(0))}

